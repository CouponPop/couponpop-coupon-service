buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.14.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.11'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version '11.14.0'
}

ext {
    set('springCloudVersion', "2024.0.0")
}

dependencyManagement {
    imports {
        mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:3.1.1"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

flyway {
    configFiles = ["$rootDir/conf/flyway.conf"]
    locations = ['filesystem:src/main/resources/db/migration']
    baselineOnMigrate = true
    baselineVersion = 1
    cleanDisabled = false
}

group = 'com.couponpop'
version = '0.0.1-SNAPSHOT'
description = 'couponpop-coupon-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// .env로부터 GITHUB_ACTOR와 GITHUB_TOKEN을 읽어오는 함수
def loadGithubCredentials() {
    def credentials = [:]
    def envFile = file('.env')
    def foundKeys = 0

    if (envFile.exists()) {
        envFile.eachLine { line ->
            if (foundKeys == 2) return // 두 키 다 찾으면 조기 종료
            if (!line.startsWith('#') && line.contains('=')) {
                def (key, value) = line.split('=', 2)*.trim()
                if (key in ['GITHUB_ACTOR', 'GITHUB_TOKEN']) {
                    if ((value.startsWith('"') && value.endsWith('"')) || (value.startsWith("'") && value.endsWith("'"))) {
                        value = value.substring(1, value.length() - 1)
                    }
                    credentials[key] = value
                    foundKeys++ //
                }
            }
        }
    }

    return credentials
}

repositories {
    mavenCentral()

    def githubCredentials = loadGithubCredentials()
    def githubUsername = githubCredentials.GITHUB_ACTOR ?: System.getenv("GITHUB_ACTOR") ?: "unknown"
    def githubToken = githubCredentials.GITHUB_TOKEN ?: System.getenv("GITHUB_TOKEN") ?: "unknown"

    maven {
        url = uri("https://maven.pkg.github.com/CouponPop/couponpop-security-module")
        credentials {
            username = githubUsername
            password = githubToken
        }
    }

    maven {
        url = uri("https://maven.pkg.github.com/CouponPop/couponpop-core-module")
        credentials {
            username = githubUsername
            password = githubToken
        }
    }
}

dependencies {
    // [CouponPop Module]
    implementation 'com.couponpop:couponpop-security:0.0.1-SNAPSHOT'
    implementation 'com.couponpop:couponpop-core:0.0.14-SNAPSHOT'

    // [Spring Boot Starter]
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // [RabbitMQ]
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    // [Spring Cloud Starter]
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // [DB]
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // [Querydsl]
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // [Flyway]
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Redisson
    implementation 'org.redisson:redisson-spring-boot-starter:3.29.0'

    // [Jackson]
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // [Restdocs]
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.4'

    // [AWS]
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

    // [Micrometer Prometheus Registry]
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // [Compile & Runtime & Test]
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
