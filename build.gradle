buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.14.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.flywaydb.flyway' version '11.14.0'
}

flyway {
    configFiles = ["$rootDir/conf/flyway.conf"]
    locations = ['filesystem:src/main/resources/db/migration']
    baselineOnMigrate = true
    baselineVersion = 1
    cleanDisabled = false
}

jacoco {
    toolVersion = "0.8.14"
}

group = 'com.couponpop'
version = '0.0.1-SNAPSHOT'
description = 'couponpop-coupon-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // [Flyway]
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

def jacocoExcludes = [
        // --- Spring Boot/JPA 관련 ---
        '**/configuration/**',
        '**/config/**',
        '**/dto/**',
        '**/exception/**',
        '**/error/**',
        '**/filter/**',
        '**/handler/**',
        '**/security/**',
        '**/util/**',

        // --- QueryDSL 관련 ---
        '**/Q*.class',

        // --- 기타 생성 코드 ---
        '**/*Application.class',
        '**/Test*.class',

        // --- Flyway 관련 ---
        '**/db/migration/**',

        // --- Swagger 관련 ---
        '**/static/docs/**',
]

afterEvaluate {
    def configureExclusions = {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: jacocoExcludes)
                })
        )
    }
    tasks.named('jacocoTestReport', JacocoReport).configure(configureExclusions)
    tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification).configure(configureExclusions)
}

tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation.set(layout.buildDirectory.dir('jacocoHtml'))
    }
}

tasks.named("jacocoTestCoverageVerification", JacocoCoverageVerification) {
    dependsOn tasks.named('jacocoTestReport')
    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                minimum = 0.60
            } // BUNDLE 기준 line covered ratio 60% 이상
        }
    }
}

tasks.named('build') {
    dependsOn 'jacocoTestCoverageVerification'
    doLast {
        println "Build completed with coverage verification"
    }
}